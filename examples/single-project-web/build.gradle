group = 'rm.examples'
buildDir = '.build'
version = '0.0.1'

buildscript {
  repositories {
    mavenLocal()
    mavenCentral() //I'll publish here later
  }
  dependencies {
    classpath(group: 'rm.tools', name: 'nw-gradle', version: '1.+')
  }
}

apply plugin: 'java'
apply plugin: 'nw-web'
apply plugin: 'eclipse-wtp'

repositories {
  mavenLocal()
  mavenCentral()
}
dependencies {
  //not included in war
  testCompile('junit:junit:4.+')
  providedCompile('javax.servlet:servlet-api:2.5', 'javax.servlet:jstl:1.1.2')
  //included in the war
  compile('org.apache.commons:commons-lang3:3.4')
}
eclipse {
  wtp {
    facet {
      facet name: 'jst.web', version: '2.5'
      facet name: 'java', version: '1.6'
    }
    component {
      resource sourcePath: '/WebContent', deployPath: '/'
    }
  }
}
compileJava {
  options.compilerArgs << '-nowarn'
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6
  //options.compilerArgs << '-XDignore.symbol.file'
}
war {
  webXml = project.file('WebContent/WEB-INF/web.xml')
  archiveName = project.name + '.war' //no version to match NWDS. good idea or bad?
  destinationDir = rootProject.file('dist')
  rootSpec.eachFile {
    println(" +> $it.name")
  }
}
nwear {
  //everything here is optional, just as examples
  def vendor = 'rm'
  archiveName = project.name + '.'  + extension
  destinationDir = rootProject.file('dist')
  manifest {
    attributes("Implementation-Vendor-Id": vendor)
  }
  sapManifest {
    attributes("keyvendor": vendor)
  }
  exclude 'application.xml'
  rootSpec.eachFile {
    println(" +> $it.name")
  }
}

//we'll leave sourceSets as defaults (maven convention)
//feel free to change for your project by uncommenting the following
//sourceSets.main.java.srcDir 'src' //to match eclipse/NWDS defaults
